buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE"
    }
}
plugins {
    id 'war'
    id 'net.saliman.cobertura' version '2.3.2'
}
apply plugin: 'spring-boot'

group = 'org.springframework.samples'
version = '1.4.0'

description = """petclinic"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

bootRepackage {
    classifier = "boot"
    mainClass = "org.springframework.samples.petclinic.PetClinicApplication"
}

repositories {
    mavenCentral()
}
dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-devtools'
    compile group: 'javax.servlet', name: 'jstl', version: '1.2'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.4'
    compile group: 'joda-time', name: 'joda-time-hibernate', version: '1.3'
    compile group: 'joda-time', name: 'joda-time-jsptags', version: '1.1.1'
    compile group: 'org.jadira.usertype', name: 'usertype.core', version: '5.0.0.GA'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: '2.8.1'
    compile group: 'mysql', name: 'mysql-connector-java', version: '6.0.4'
    compile group: 'javax.cache', name: 'cache-api', version: '1.0.0'
    compile group: 'org.ehcache', name: 'ehcache', version: '3.1.1'
    compile group: 'com.github.dandelion', name: 'dandelion-jsp', version: '1.1.1'
    compile group: 'com.github.dandelion', name: 'datatables-jsp', version: '1.1.0'
    compile group: 'com.github.dandelion', name: 'datatables-export-itext', version: '1.1.0'
    compile group: 'org.webjars', name: 'jquery', version: '2.2.4'
    compile group: 'org.webjars', name: 'jquery-ui', version: '1.11.4'
    compile group: 'org.webjars', name: 'bootstrap', version: '3.3.6'
    runtime group: 'org.hsqldb', name: 'hsqldb', version: '2.3.3'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }
    providedCompile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-jasper', version: '8.5.4'
}

ext.dockerGroup = 'docker'
ext.dockerBuildDir = "$buildDir/docker"

task copyDockerFiles(type: Copy) {
    group = dockerGroup
    from 'docker'
    into dockerBuildDir
}

task copyWarToDocker(type: Copy, dependsOn: war) {
    group = dockerGroup
    from "$war.destinationDir/$war.baseName-$war.version-$bootRepackage.classifier.$war.extension"
    into "$dockerBuildDir/server"

    rename { "$war.baseName.$war.extension" }
}

task copyClientToDocker(type: Copy, dependsOn: ':client:buildClean') {
    group = dockerGroup
    from 'client/public'
    into "$dockerBuildDir/client/public"
}

task prepareDocker() {
    group = dockerGroup
    dependsOn copyDockerFiles, copyClientToDocker, copyWarToDocker
}

build.dependsOn prepareDocker
